# Maintainer: Jesper Jensen <delusionallogic at gmail dot com>

_pkgbase='beepy-kbd'
pkgname="${_pkgbase}-git-dkms"
pkgver=78
pkgrel=1
pkgdesc='DRM kernel driver for 2.7" 400x240 Sharp memory LCD panel.'
arch=('any')
url='https://github.com/ardangelo/beepberry-keyboard-driver'
license=('GPL2')
depends=('dkms')
makedepends=('git')
optdepends=()
provides=("${_pkgbase}")
conflicts=("${_pkgbase}")
source=(
	"${_pkgbase}::git+https://github.com/ardangelo/beepberry-keyboard-driver.git"
	'dkms.conf'
	'dkms.post_install'
	'dkms.post_remove'
	'dkms.post_build'
	'beepy-kbd.map'
)
md5sums=('SKIP'
         'bc114742ef45342bc0d88e4eab822e0a'
         '7e1d73a887c541db68f16071b10fbd0e'
         '37b2bdcd621fe15c84b034669863c95c'
         '030ddbc214e7494bb8e0c1d8a42972d0'
         '96e65bcee0319da008003bd665cfed4d')

pkgver() {
	cd "$srcdir/$_pkgbase"
	git rev-list --count HEAD
}

package() {
	install -Dm644 beepy-kbd.map "${pkgdir}"/usr/share/kbd/keymaps/beepy-kbd.map

	# Copy dkms.conf
	install -Dm644 dkms.conf "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/dkms.conf
	install -Dm755 dkms.post_install "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/dkms.post_install
	install -Dm755 dkms.post_remove "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/dkms.post_remove
	install -Dm755 dkms.post_build "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/dkms.post_build

	# Set name and version
	sed -e "s/@_PKGBASE@/${_pkgbase}/" \
		-e "s/@PKGVER@/${pkgver}/" \
		-i "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/dkms.conf

	# Copy sources (including Makefile)
	cp -r ${_pkgbase}/* "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/
}
